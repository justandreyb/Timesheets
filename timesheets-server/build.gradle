plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.3.RELEASE'
}

apply from: './dependencies.gradle'

group = "com.justandreyb.timesheets"
version = "0.0.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = "timesheets-server"
    group    = "${project.group}"
    version  = "${project.version}"
    manifest { attributes "Main-Class": "com.justandreyb.timesheets.server.TimesheetsServerApplication" }
}

task createDockerImage(type: Exec) {
    dependsOn clean
    dependsOn build
    tasks.findByName('build').mustRunAfter 'clean'

    group 'Docker'
    description 'Create docker image'
    commandLine 'docker'
    args = [
        'build','.','-t',"${project.group}/${bootJar.baseName}:latest","--build-arg", "JAR_FILE=${bootJar.archiveName}"
    ]
}

/**
 * Starting application with "application.yml" and "application-dev.yml" properties
 */
task startServer (type: JavaExec, dependsOn: classes){
    group 'Server'
    main = "com.justandreyb.timesheets.server.TimesheetsServerApplication"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            "-Dspring.profiles.active=dev"
    ]
}

/**
 * Starting application with "application.yml" and "application-prod.yml" properties
 */
task startProductionServer (type: JavaExec, dependsOn: classes){
    group 'Server'
    main = "com.justandreyb.timesheets.server.TimesheetsServerApplication"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            "-Dspring.profiles.active=prod"
    ]
}